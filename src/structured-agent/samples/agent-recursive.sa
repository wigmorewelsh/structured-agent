extern fn receive(): String

extern fn read_file(path: String): String
extern fn write_file(path: String, content: String): ()

## **overrides** the file path with the content, WARNING this replaces existing content!
fn write_file_content(path: String, content: String): String {
    write_file(path, content)
    return "Success"
}

fn edit_content(existing_content: String): String {
    "Given the existing content, what should be the new content?"!
    existing_content!
}

## edits a file by reading the file, applying the current task as an edit, then overwriting the file
fn edit_file(filename: String): String {
    let content = read_file(filename)
    let new_content = edit_content(content)
    write_file(filename, new_content)

    return "Completed editing file"
}

fn is_task_complete(): Boolean {
    "Is the task complete?"!
}

fn agent_loop(): () {
    "what is the next action to perform?"!

    let result = select {
        edit_file(_) as r => r,
        read_file(_) as r => r,
        write_file_content(_, _) as r => r
    }

    result!

    if is_task_complete() {
        return ()
    } else {
        return agent_loop()
    }
}

fn plan(): String {
    '''
    Given the task above where should I start?
    What are the high level actions without implementation detail?

    What can I do? And what can I gain by doing so?
    '''!
}

fn main(): () {
    '''
    You are running as a structured agent, the process is defined in a programing language that you dont have access to.
    You will be asked to SELECT between tools/functions and generate content based on input.
    Youll have to decide what to use in a given context.

    Assume that files already contain content and writing to the replaced the content they already contain.
    This is a distructive change and should be done with care not to accidently replace content.

    You have no access to programing languages or terminal tools.
    That means `echo`, `sed`, `awk`, `python`, `node` etc are not avalaible.
    '''!

    # let task = "read the file test.tmp"
    # let task = "add \"SUCCESS\" to the start of the file test.tmp"
    # let task = "combine the content in test1.tmp and test2.tmp and save the results in test.tmp"
    let task = receive()
    task!

    plan()!

    agent_loop()
}
